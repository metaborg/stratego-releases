module parse-unit-io
signature
  constructors
    parse-test : Description * Input * Result -> ParseTest
    parse-test : Input * Result -> ParseTest
    string     : String -> Input

strategies

  read-parse-testsuite =
    xtc-transform(!"sglri", !["-p", <xtc-find> "parse-testsuite.tbl", "--start", "TestSuite", "--amb", "off" | <pass-verbose> ()])
    ; read-from
    ; parse-testsuite(id, try(topsort-to-options), id)
    ; parse-testsuite(id, id, map(drop-whitespace))
    ; topdown(try(drop-quotes))
    ; parse-testsuite(id, id, map(try(add-description)))
    ; parse-testsuite(id, id, map(ensure-test-input-absolute-path))
    ; parse-testsuite(id, id, map(ensure-test-output-not-in-file))

  topsort-to-options :
    topsort(s) -> options([option("topsort", s)])

rules

  drop-whitespace :
    parse-test(_, descr, input, result) -> parse-test(descr, input, result)

  drop-whitespace :
    parse-test(_, input, result) -> parse-test(input, result)

  drop-quotes :
    string(_, content, _) -> string(content)

  add-description :
    parse-test(string(s), output) -> parse-test(description(s), string(s), output)

  add-description :
    parse-test(file(f), output) -> parse-test(description(f), file(f), output)

rules

  ensure-test-input-absolute-path =
    ?parse-test(d, string(s), o)

  /**
   * If the test input is a file then make it a complete path.
   */
  ensure-test-input-absolute-path =
    parse-test(id, file(abs-test-file), id)

  /**
   * Ensures that the test output is not in a file.
   */
  ensure-test-output-not-in-file =
    if ?parse-test(_, _, file(_)) then
      read-test-output-to-pattern
    end

  read-test-output-to-pattern :
    parse-test(desc, input, file(f)) -> parse-test(desc, input, pattern(p))
    where
      p := <read-from; explode-aterm> FILE(f)
      <+ if-verbose7(!f ; debug(!"Reading aterm result failed: "))
