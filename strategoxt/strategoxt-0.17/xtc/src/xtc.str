module xtc
imports
  libstratego-lib
  xtc-lib
  xtc-query

strategies

  xtc = 
    xtc-xtc-call
    <+ xtc-xtc-get
    <+ xtc-main

  xtc-main = 
    parse-options(xtc-options, xtc-usage, xtc-usage)
    ; xtc-location
    ; log(|Notice(),<conc-strings>("Using repository ",<id>))
    ; xtc-read
    ; ((<get-config> "register"; xtc-register
        <+ <get-config> "import"; register-import)
       ; xtc-store
       <+ <get-config> "query"; xtc-query)
    ; (<get-config> "--exit-code" < exit + <exit> 0)
   <+ err(|"xtc failed! ")

  xtc-xtc-get =
    ?[self, "get" | args]
    ; <fprint> (stdout(), <map(xtc-find)> args)
    ; <fprint> (stdout(), ["\n"])
    ; <exit> 0

  xtc-xtc-call =
    ?[self, "call", prog | args]
    ; <execvp>(<xtc-find> prog, args)

/**
 * Options
 */
strategies

  xtc-options =
    ArgOption("-r" + "--repository",
        where(<set-config>("-r", <id>)),
        !"--repository rep | -r rep    Tool repository")

  + ArgOption("i" + "imp" + "import",
        where(<extend-config>("import", [<id>])),
        !"import r | i r               Import repository r\n")

  + Option("r" + "reg" + "register",
        where(<set-config>("register", <id>)),
        !"register | r                 Register a tool")

  + ArgOption("-t" + "--tool",
        where(<extend-config>("-t", [<id>])),
        !"--tool n | -t n              Tool name")

  + ArgOption("-l" + "--location",
        where(<set-config>("-l", <id>)),
        !"-l loc  | --location loc     Location of tool (directory)")

  + ArgOption("-p" + "--path",
        where(<set-config>("--path", <id>)),
        !"-p path | --path path        Location of tool (full path)")

  + ArgOption("-V" + "--Version",
        where(<set-config>("-V", <id>)),
        !"--Version n | -V n           Version of tool\n")

  + Option("q" + "query",
        where(<set-config>("query", <id>)),
        !"query | q                    Query the XTC repository")

  + Option("-a" + "--all",
        where(<set-config>("-a", ())),
        !"--all | -a                   List all registered tools")

  + Option("-L" + "--Location",
        where(<set-config>("-l", ())),
        !"--Location | -L              List only location of tool")

  + Option("-R" + "--Repository",
        where(<set-config>("-R", ())),
        !"--Repository | -R            Location of repository\n")

    // This option will never be handled: it's only for printing some info in --help
  + ArgOption("get"
      , fail
      , !"get tool...                  Query the standard XTC repository for all the specified tools\n"
    )

  + ArgOption("--verbose",
        where(<set-config> ("--verbose", <verbose-to-int>)),
        !"--verbose i                  Verbosity level i (default 1)")


strategies

  xtc-usage =
    default-system-usage(xtc-short-description, xtc-long-description)

  xtc-short-description =
    !["Usage: xtc [-r rep] register -t tool -V vers -l loc\n",
      "       xtc [-r rep] (import r)+\n",
      "       xtc [-r rep] query (-a | -t tool [-V version]) [-L]"]
 
  xtc-long-description =
    !["   The xtc command supports the registration of tools in an XTC repository\n",
      "   and querying XTC repositories"]
