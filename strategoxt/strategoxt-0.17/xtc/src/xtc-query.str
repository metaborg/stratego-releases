module xtc-query
imports xtc-lib
strategies

  xtc-query = 
    repository-query
    <+ all-query
    <+ tool-query

  repository-query =
    <get-config> "-R"
    ; xtc-location
    ; <fprint> (stdout(), ["XTC_REPOSITORY=", <id>, "\n"])

  all-query =
    <get-config> "-a"
    ; xtc-import
    ; <table-getlist> XTC()
    ; map(try((Tool(id),id); list-tool))

  list-tool =
    ?(Tool(tool), <id>)
    ; map({version,loc:
        ?(version, loc)
        ; ( <get-config> "-l"
            ; <fprint> (stdout(), [loc, "\n"])
            <+ <fprint> (stdout(), [tool, " (", version, ") : ", loc, "\n"])
          )
      })

  tool-query =
    <get-config> "-t"
    ; xtc-import
    ; map({tool:
        ?tool
        ; (  log(|Vomit(),<conc-strings>("Retrieving ",<id>))
          ; <table-get> (XTC(), Tool(tool)) => entries
          ; <version-query <+ list-tool> (Tool(tool), entries)
          <+ <fprint> (stderr(), [tool, " : no registration", "\n"])
             ; <set-config> ("--exit-code", 1)
          )
      })

  version-query =
    where(<get-config> "-V" => version)
    ; (?Tool(tool), fetch(?(version, loc)))
    ; <list-tool> (Tool(tool), [(version,loc)])
