module xml-aterm-tests
imports 
  libstratego-lib
  libstratego-xtc xml-info

strategies

  main-xml-aterm-tests =
    option-wrap(general-options,
      test-suite(!"
===============================================
              xml aterm tests
===============================================",

        implicit-representation-tests
        
      ; implicit-roundtrip-tests
      ; explicit-roundtrip-tests
      ; very-explicit-roundtrip-tests
      )
    )

strategies

  implicit-representation-tests =
    {| Explicit :
      rules(Explicit : () -> "--implicit")
    ; say(!"
======== Implicit representation tests ========")

    ; representation-test(|Fred(),      " <Fred/>")
    ; representation-test(|Bar("foo"),  " <Bar>foo</Bar>")
    ; representation-test(|Bar(123),    " <Bar>123</Bar>")
    ; representation-test(|Bar(Fred()), " <Bar><Fred/></Bar>")
    ; representation-test(|Foo(Fred(), Fred()), " <Foo><Fred/><Fred/></Foo>")
    |}


strategies

  very-explicit-roundtrip-tests =
    {| Explicit :
      rules(Explicit : () -> "--very-explicit")
    ; say(!"
======== Very explicit roundtrip tests ========")

    ; easy-roundtrip-tests
    ; avg-roundtrip-tests
    ; difficult-roundtrip-tests
    |}

  explicit-roundtrip-tests =
    {| Explicit :
      rules(Explicit : () -> "--explicit")
    ; say(!"
========== Explicit roundtrip tests ===========")

    ; easy-roundtrip-tests
    ; avg-roundtrip-tests
    ; difficult-roundtrip-tests
    |}

  implicit-roundtrip-tests =
    {| Explicit :
      rules(Explicit : () -> "--implicit")
    ; say(!"
========== Implicit roundtrip tests ===========")

    ; easy-roundtrip-tests
    |}

  easy-roundtrip-tests =
      roundtrip-test(| Fred())
    ; roundtrip-test(| Bar("1"))
    ; roundtrip-test(| Foo(Fred(), Bar("1")))
    ; roundtrip-test(| Foo(Fred(), Fred()))
    ; roundtrip-test(| Fred(){Fred()} )
    ; roundtrip-test(| Fred(){Bar("1")} )
    ; roundtrip-test(| Fred(){Foo(Fred(), Bar("1"))} )
    ; roundtrip-test(| Fred(){Foo(Fred(), Fred())} )

  avg-roundtrip-tests =
      roundtrip-test(|1)
    ; roundtrip-test(|2)
    ; roundtrip-test(|"a")
    ; roundtrip-test(|"ab")
    ; roundtrip-test(|" ab ")
    ; roundtrip-test(| [1, 2])
    ; roundtrip-test(| [1, 2, 3])
    ; roundtrip-test(| ["1", "2    ", " 3 "])
    ; roundtrip-test(| Foo(1, 2))
    ; roundtrip-test(| Foo(3, 4))
    ; roundtrip-test(| Foo(1, [2, 3]))
    ; roundtrip-test(| Foo([1, 2], [3, 4]))
    ; roundtrip-test(| Some(4))
    ; roundtrip-test(| None())
    ; roundtrip-test(| Element(Name(None(), "foo"), [], []))

  difficult-roundtrip-tests =
      roundtrip-test(| Foo(1, [2, 3]){Foo(1, 2)})
    ; roundtrip-test(| Foo(1, [2, 3]){1})
    ; roundtrip-test(| Foo(1, [2, 3]){2})
    ; roundtrip-test(| Foo(1, [2, 3]){"2"})
    ; roundtrip-test(| int(5))
    ; roundtrip-test(| string("voodoo"))
    ; roundtrip-test(| None())
    ; roundtrip-test(|"a"{1})
    ; roundtrip-test(|"a"{1, 2})
    ; roundtrip-test(|"a"{(1, 2)})
    ; roundtrip-test(| 1{1})
    ; roundtrip-test(| [1, 2]{1})
    ; roundtrip-test(| (1, 2){1, 2})

strategies

  representation-test(|t, xml) =
    apply-test(!t,
      xtc-temp-files(
        write-to
      ; xtc-transform(!"aterm2xml", ![<Explicit> () | <pass-verbose>])
      ; xtc-transform(!"parse-xml-info", pass-verbose)
      ; read-from
      )
    , !t
    , <print-to> [xml]
      ; xtc-transform(!"parse-xml-info", pass-verbose)
      ; read-from
    )

  roundtrip-test(|t) =
    apply-test(!t,
      xtc-temp-files(
        write-to
      ; xtc-transform(!"aterm2xml", ![<Explicit> () | <pass-verbose>])
      ; xtc-transform(!"xml2aterm", ![<Explicit> () | <pass-verbose>])
      ; read-from
      )
    , !t
    , !t
    )

signature
  constructors
    Foo : Foo * Foo -> Foo
    Bar : Foo -> Foo
    Fred : Foo
    int : Int -> Foo
    string : String -> Foo
