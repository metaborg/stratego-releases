/**
 * @author Merijn de Jonge (mdjonge@cwi.nl)
 */
module Abox-2-html-lib
imports
  pp-table
  misc

/**
 * These are constructors for HTML constructs that are used for the BOX to
 * HTML translation
 */
signature
  constructors
   TABLE: Opts * Rows -> Table
   TR   : Opts * Cels -> TableRpw
   TD   : Opts * Text -> TableData
   TD'  : Text -> Td'
   SPAN : Opts * Text -> Span
   HREF : Url  * Text -> Href
   ANCHOR : Url -> Anchor
   BR   : Br
   NBSP : Nbsp
   S    : Text -> Text
   TDTD : Tdtd
   TDTR : Text -> Trtd
   PRE  : Text -> Pre
   HR   : Hr 

strategies 
html2text = 
   bottomup(try(Html2text))

// This strategy implments a simple pretty-printer for abstract HTML
Html2text =
   ?TABLE( opts, rows );
      ![ "<table align=\"left\" border=\"0\" cellspacing=\"0\" cellpadding=\"0\"", opts, ">",
      rows, "</table>\n"]
   <+
      ?TR( opts, cells );
      !["<tr ", opts, ">", cells, "</tr>\n"]
   <+
      ?TD( opts, table-data );
      !["<td ", opts, ">", table-data, "</td>"]
   <+
      ?TD'( text );
      !["</td>", text, "<td>"]
   <+
      ?SPAN( opts, text );
      !["<span ", opts, ">",text,"</span>"]
   <+
      ?PRE( text );
      !["<pre>", text, "</pre>"]
   <+
      ?TDTR( text );
      !["</td></tr>", text, "<tr><td>"]
   <+
      ?HREF( url, text );
      !["<a href=\"", url, "\">", text, "</a>"]
   <+
      ?ANCHOR( url );
      !["<a name=\"", url, "\"></a>"]
   <+
      ?BR(); !"<br>"
   <+
      ?NBSP(); !"&nbsp;"
   <+
      ?HR(); !"<hr>"
   <+
      ?TDTD();!"</td><td>"

html-string =
   explode-string;
   listtd( try( replace-quotes ) );
   map(try(
      \32 -> <explode-string>"&nbsp;" \
   +  \38 -> <explode-string>"&amp;" \
   +  \60 -> <explode-string>"&lt;" \
   +  \62 -> <explode-string>"&gt;" \
   ));
   flat-list;
   implode-string

// replaces "abcd\"efg\"hij" to "abcd``efg''hij"
replace-quotes =
   [34|?xs]; 
   ![96,96| <oncetd( \ [34| xs'] -> [39,39|xs'] \ ) >xs]
