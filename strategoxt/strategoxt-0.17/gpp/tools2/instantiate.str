/**
 * @author Merijn de Jonge (mdjonge@cwi.nl)
 */
module instantiate
imports pp-table
rules

  instantiate =
    ?(template, args)
    ; <instantiate(|args)> template

  instantiate(|args) =
    bottomup(
      try(
        ?Arg(_); Instantiate(|args)
      + ?Arg2(_, _); Instantiate(|args)
      + ?[_|_]; flat-list
      )
    )
   
  Instantiate(|args) =
    FastInstantiate(|args) <+ SlowInstantiate(|args)
    
  FastInstantiate(|args) :
    Arg(1) -> a
    where
      !args => [a | _]
      
  FastInstantiate(|args) :
    Arg(2) -> a
    where
      !args => [_, a | _]
      
  FastInstantiate(|args) :
    Arg(3) -> a
    where
      !args => [_, _, a | _]

  SlowInstantiate(|args) :
    Arg(n) -> <index(|n)> args

  SlowInstantiate(|args) :
    Arg2(n, m) -> <index(|m)> list
    where
      <index(|n)> args => list

strategies

  instantiate-list(|pp-entry) =
    rec rec(
      {sym,xs:
         []
      <+ ?[sym|xs]
         ; [<instantiate(|[sym])> pp-entry | <rec> xs ]
      }
    )
  
  instantiate-sep-list(|pp-entry) =
    rec rec(
      {sym,sep,xs:
         []
      <+ ?[_]
      <+ ?[sym, sep | xs]
         ; [<instantiate(|[sym, sep])> pp-entry | <rec> xs ]
      }
    )
