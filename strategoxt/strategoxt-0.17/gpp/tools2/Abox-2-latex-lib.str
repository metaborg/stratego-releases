/**
 * @author Merijn de Jonge (mdjonge@cwi.nl)
 */
module Abox-2-latex-lib
signature
  constructors
    ALLTT    : Boxes                            -> ALLTT
    BOXENV   : Width    * Boxes                 -> BOXENV
    HBOX     : HS       * Boxes                 -> Hbox
    VBOX     : VS       * IS    * Boxes         -> Vbox
    HVBOX    : HS       * VS    * IS    * Boxes -> HVbox
    ABOX     : TableDef * Boxes                 -> Abox
    ALTBOX   : Box1     * Box2                  -> ALTBOX
    CBOX     : Boxes                            -> Cbox
    LBOX     : Fmt      * Box                   -> LBOX
    LBLBOX   : String   * Box                   -> LBLBOX
    REFBOX   : String   * Box                   -> REFBOX
    C        : Alignment
    R        : Alignment
    L        : Alignment
    EOR      : Endofrow
    CSEP     : Colsep
    BOXFONT  : Font * Box -> BoxFont
    BOXCOLOR : Color -> Bpxfont

strategies

  latex2text =
      bottomup(try(Latex2text ) )

  Latex2text =
        ?ALLTT(boxes);                !["\\begin{alltt}\n", boxes, "\n\\end{alltt}\n"]
     <+ ?BOXENV( [], boxes );         !["\\begin{boxenv}%\n", boxes , "%\n\\end{boxenv}\n"]
     <+ ?BOXENV( [width], boxes );    !["\\begin{boxenv}{", width, "}%\n", boxes , "%\n\\end{boxenv}\n"]
     <+ ?HBOX( hs, boxes );           !["\\begin{HBOX}{", hs, "}%\n", boxes, "%\n\\end{HBOX}%\n"]
     <+ ?VBOX( vs, is, boxes );       !["\\begin{VBOX}{", vs, "}{", is, "}%\n", boxes, "%\n\\end{VBOX}%\n"]
     <+ ?HVBOX( hs, vs, is, boxes );  !["\\begin{HVBOX}{", hs, "}{", vs, "}{", is, "}%\n", boxes, "%\n\\end{HVBOX}%\n"]
     <+ ?ABOX( tdef, boxes );         !["\\begin{ABOX}{", tdef, "}%\n", boxes, "\\crcr%\n\\end{ABOX}%\n"]
     <+ ?ALTBOX( b1, b2 );            !["\\begin{ALTBOX}{%\n", b1, "%\n}", b2, "%\n}%\n\\end{ALTBOX}%\n"]
     <+ ?CBOX( b );                   !["%\n\\begin{latextext}%\n", b, "%\n\\end{latextext}%\n"]
     <+ ?LBOX( s, b ); <is-int>s;     !["\\\ncopies{", <int-to-string>s, "}{", b, "}%"]
     <+ ?LBOX("=", b );               !["\\begin{LBOX}{=}%\n", b, "%\n\\end{LBOX}%\n"]
     <+ ?LBOX( s, b );                !["\\begin{LBOX}{", s, "}%\n", b, "%\n\\end{LBOX}%\n"]
     <+ ?LBLBOX( l, b );              !["\\boxlabel{", l, "}", b]
     <+ ?REFBOX( l, b );              !["\\boxref{", l, "}{", b, "}" ]
     <+ ?BOXFONT(KW, b );             !["\\KWf{", b, "}"]
     <+ ?BOXFONT(VAR, b );            !["\\VARf{", b, "}"]
     <+ ?BOXFONT(NUM, b );            !["\\NUMf{", b, "}"]
     <+ ?BOXFONT(MATH, b );           !["\\MATHf{", b, "}"]
     <+ ?BOXFONT("sf", b );           !["\\textsf{", b, "}"]
     <+ ?BOXFONT("rm", b );           !["\\textrm{", b, "}"]
     <+ ?BOXFONT("tt", b );           !["\\texttt{", b, "}"]
     <+ ?BOXFONT("md", b );           !["\\textmd{", b, "}"]
     <+ ?BOXFONT("bf", b );           !["\\textbf{", b, "}"]
     <+ ?BOXFONT("up", b );           !["\\textup{", b, "}"]
     <+ ?BOXFONT("it", b );           !["\\textit{", b, "}"]
     <+ ?BOXFONT("sc", b );           !["\\textsc{", b, "}"]
     <+ ?BOXFONT("sl", b );           !["\\textsl{", b, "}"]
     <+ ?BOXFONT("em", b );           !["\\emph{", b, "}"]
     <+ ?BOXFONT("tiny", b );         !["\\tiny{", b, "}"]
     <+ ?BOXFONT("scriptsize", b );   !["\\scriptsize{", b, "}"]
     <+ ?BOXFONT("footnotesize", b ); !["\\footnotesize{", b, "}"]
     <+ ?BOXFONT("small", b );        !["\\small{", b, "}"]
     <+ ?BOXFONT("normalsize", b );   !["\\normalsize{", b, "}"]
     <+ ?BOXFONT("large", b );        !["\\large{", b, "}"]
     <+ ?BOXFONT("Large", b );        !["\\Large{", b, "}"]
     <+ ?BOXFONT("LARGE", b );        !["\\LARGE{", b, "}"]
     <+ ?BOXFONT("huge", b );         !["\\huge{", b, "}"]
     <+ ?BOXFONT("Huge", b );         !["\\Huge{", b, "}"]
     <+ ?BOXFONT(BOXCOLOR(c),b);      !["\\textcolor{", c, "}{", b, "}"]
     <+ ?C();                         !"\\hfill#\\hfill"
     <+ ?R();                         !"\\hfill#"
     <+ ?L();                         !"#\\hfill"
     <+ ?EOR();                       !"\\cr"
     <+ ?CSEP();                      !"&"

  latex-string = 
        latex-abbrev 
     <+ string-as-chars(escape-special-characters;latex-quotes)

  latex-abbrev = <table-get> ("latex-abbrevs", <id>)

  escape-special-characters =
   map(try(
            \32 -> <explode-string>"\\boxspace{}" \ /* space */
         +  \35 -> <explode-string>"\\#" \
         +  \36 -> <explode-string>"\\$" \
         +  \37 -> <explode-string>"\\%" \
         +  \38 -> <explode-string>"\\&" \
         +  \45 -> <explode-string>"\\mbox{-}" \ /* - */
         +  \60 -> <explode-string>"\\ensuremath{<}" \
         +  \62 -> <explode-string>"\\ensuremath{>}" \
         +  \92 -> <explode-string>"\\ensuremath{\\backslash}" \
         +  \94 -> <explode-string>"\\^{}" \
         +  \95 -> <explode-string>"\\_" \
         + \123 -> <explode-string>"\\{" \
         + \124 -> <explode-string>"\\ensuremath{\\vert}" \
         + \125 -> <explode-string>"\\}" \
         + \126 -> <explode-string>"\\~{}" \
        ))
   ; flat-list

  latex-quotes =
     listtd( try( replace-quotes ) )
            
  // replaces "abcd\"efg\"hij" to "abcd``efg''hij"
  replace-quotes:
    ['"' | xs ] -> ['`', '`' | <at-suffix( \ ['"'| xs'] -> ['\'', '\'' | xs'] \ ) > xs]
