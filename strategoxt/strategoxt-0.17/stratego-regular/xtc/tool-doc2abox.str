module tool-doc2abox
imports tool-doc Box libstratego-lib
	libstratego-xtc

strategies

  main-tool-doc2abox =
    io-wrap(docsections-to-box)

strategies

  docsections-to-box :
    docsections -> box |[ V vs=1 [ ~*bs  ] ]|
      where <map(try-docsection-to-box)> docsections => bs

  try-docsection-to-box =
    docsection-to-box <+ debug(!"Not a docsection: "); fail

  docsection-to-box :
    HSection(s, sections) -> box |[ V is=3 vs=0 [~lit:s V vs=1 [~*bs]] ]|
      where <map(try-docsection-to-box)> sections => bs

  docsection-to-box :
    Paragraph(s) -> box |[ HV hs=1 vs=0 [ ~*tokens ] ]|
      where < <string-tokenize> ([' ', '\n', '\r', '\t'], <id>)
            ; map( !box |[ ~lit:<id> ]| )
            > s => tokens

  docsection-to-box :
    Line(s) -> box |[ ~lit:s ]|

  docsection-to-box :
    ItemList(sections) -> box |[ V vs=0 [ ~*bs ] ]|
      where <map(
               try-docsection-to-box
             ; !box |[ H hs=1 ["*" ~<id> ] ]|
             )> sections => bs

  docsection-to-box :
    DefList(defs) -> box |[ A (l hs=2,l hs=0) [ ~*bs ] ]|
      where <map(def-to-row)> defs => bs

  def-to-row :
    Def(k, v) -> box |[ R [ ~lit:k ~b ] ]|
      where <try-docsection-to-box> v => b
