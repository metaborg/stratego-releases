module asfix-yield
imports libstratego-lib AsFix
strategies

  io-asfix-yield =
    parse-options(io-options, asfix-yield-usage, system-about)
    ; (<fopen> (<get-config> "-o", "w") <+ stdout-stream) => stream
    ; input(asfix-yield(|stream))
    ; <fclose> stream
    ; <exit> 0
    
  asfix-yield-usage =
    default-system-usage(
      !["Usage: asfix-yield [options]"]
    , !["  asfix-yield unparses an asfix tree to flat text."]
    )

  asfix-yield(|stream) = 
    if is-asfix2 then
      asfix-yield2(|stream)
    else
      <fatal-error> ["Input is not in AsFix format."]
    end

  asfix-yield2(|stream) = 
    leaves(
         <fputs> (<is-string>, stream)
      <+ <fputc> (<id>, stream)
    , is-leaf
    , asfix-skip2
    )
    
  asfix-skip2(x) = 
      appl(id, list(x))
    + amb([x|id])
    + parsetree(x,id)
    // + flatlex(id,x)

  is-asfix2 =
      parsetree(id, id)
    + appl(id,id)
