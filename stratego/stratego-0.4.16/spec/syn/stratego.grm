%{
#include <aterm1.h>
#include "stratego.h"

extern int yylineno;
extern char file_name[256];
extern FILE *yyin;

ATerm parse_tree;

void yymessage(char *msg)
{
  fprintf(stderr, "%s: line %d - %s\n", file_name, yylineno + 1, msg);
}

void yyerror(char *msg)
{
  yymessage(msg);
  exit(1);
}

%}

%union{
   int    num;
   double real;
   char   *string;
   ATerm  term;
   ATermList list;
}

%{

/* int yylex(void); */
 
int yylex(YYSTYPE *lvalp);
int yyparse(void);

int parse()
{
   int result;
   result = yyparse();
   return result;
}
%}

%token <term> LCID
%token <term> STRINGTOK 
%token <term> UCID
%token <term> ID

%token ARROW 
%token LONGARROW 
%token ASSIGN
%token ASTERISK 
%token BACKARROW 
%token BAR
%token BUILD
%token AS
%token ANNBUILD
%token ANNRM
%token BUILDe 
%token COMMA 
%token EQ 
%token FAIL 
%token GG
%token IMPORTS 
%token KIDS
%token LBRACK 
%token LCURLY 
%token LL 
%token LPAREN 
%token LT 
%token GT
%token ANNMATCH 
%token MATCH 
%token MATCHe
%token MODULE
%token NEW 
%token OPERATIONS 
%token OVERLAYS
%token PARSEPROG 
%token PARSEQUERY
%token PRIM
%token RCURLY 
%token RPAREN 
%token RULES 
%token SIGNATURE 
%token SORTS 
%token STARSTAR
%token STRATEGIES 
%token STR_GT 
%token SUCC 
%token UNDERSCORE 
%token DOT
%token BACKSLASH
%token EXPLODE
%token MODIFIER

%right COLON DOUBLECOLON
%right PLUS LTPLUS
%right SEMICOLON 
%right DOUBLEARROW 
%right LEFTDOUBLEARROW 
%left  ASSIGN
%right RBRACK
%right NOT WHERE TEST ONE ALL THREAD SOMETOK MU 
%right <real> REAL 
%right <num> INT 

%type <term> decl 
%type <list> decls 
%type <term> id 
%type <list> idlist 
%type <term> kind
%type <list> kinds
%type <list> mods
%type <term> opdecl
%type <list> opdecls
%type <term> optcond 
%type <list> optcont 
%type <term> optkind
%type <list> optstrategylist 
%type <list> opttermlist 
%type <list> optvarlist 
%type <list> overlays
%type <term> overlay
%type <term> rule 
%type <term> stratrule 
%type <term> rule_def 
%type <list> rules 
%type <term> sdecl
%type <list> sdecls
%type <term> start
%type <list> strategies 
%type <term> strategy 
%type <term> optapplication
%type <term> strategy_def 
%type <list> strategylist
%type <term> strategytail 
%type <term> tail
%type <term> term 
%type <list> termlist 
%type <term> trav
%type <list> tvarlist 
%type <term> type
%type <list> typelist
%type <list> varlist

%start start

%pure_parser
   
%%
 
start 	: MODULE id decls 	       	{parse_tree = ATmake("Specification([<list>])", $3);}
        | LBRACK strategy RBRACK term 	{parse_tree = App2("Trm", $2, $4);}
	| strategy			{parse_tree = App1("Strategy",$1);}
	;
  
decls	: decl	     			{$$ = ATmakeList1($1);}
	| decls decl 			{$$ = ATappend($1, $2);}
 	|				{$$ = ATmakeList0();}
        ;

mods	: 				{$$ = ATmakeList0();}
	| mods id			{$$ = ATappend($1, $2);}
	;

decl	: IMPORTS mods			{$$ = App1("Imports", (ATerm) $2);}
	| RULES rules 			{$$ = App1("Rules", (ATerm) $2);}
	| STRATEGIES strategies 	{$$ = App1("Strategies", (ATerm) $2);}
	| SIGNATURE sdecls		{$$ = App1("Signature", (ATerm) $2);}
	| OVERLAYS overlays		{$$ = App1("Overlays", (ATerm) $2);}
        ;

sdecls	: sdecl				{$$ = ATmakeList1($1);}
	| sdecls sdecl 			{$$ = ATappend($1, $2);}
	| 				{$$ = ATmakeList0();}
        ;

sdecl	: SORTS idlist			{$$ = App1("Sorts", (ATerm) $2);}
	| OPERATIONS opdecls		{$$ = App1("Operations", (ATerm) $2);}
        ; 

idlist  :                   		{$$ = ATmakeList0();}
        | idlist id optkind             {$$ = ATinsert($1, App2("Sort", $2, $3));}

optkind : 				{$$ = ATmake("Nokind");}
	| LPAREN termlist RPAREN 	{$$ = App1("Kinds", (ATerm) $2);}
        ;

kinds   : kind 				{$$ = ATmakeList1($1);}
	| kinds kind 			{$$ = ATappend($1, $2);}
        ;

kind	: ASTERISK			{$$ = ATmake("Type");}
	| STARSTAR			{$$ = ATmake("TypeList");}
        ;

opdecls : 				{$$ = ATmakeList0();}
	| opdecls opdecl 		{$$ = ATappend($1, $2);}
        ;

opdecl	: id COLON type			{$$ = App2("OpDecl", $1, $3);}
        ;

type 	: typelist ARROW term 		{$$ = App2("FunType", (ATerm) $1, $3);}
	| term				{$$ = App1("ConstType", $1);}
        ;

typelist
	: typelist ASTERISK term 	{$$ = ATappend($1, $3);}
	| term				{$$ = ATmakeList1($1);}
        ;

id      : LCID 				{$$ = $1;}
   	| UCID 				{$$ = $1;}
        ;

/* Terms */

term 	: id optcont		      	{if(ATisEmpty($2)) 
					   $$ = App1("Var", $1);
					 else
					   $$ = App3("Con", 
						      App1("Var", $1), ATgetFirst($2), 
						      ATgetFirst(ATgetNext($2)));}
						
	| UNDERSCORE			{$$ = ATmake("Wld");}
					
	| UNDERSCORE term		{$$ = App1("BuildDefault", $2);}

	| INT			      	{$$ = App1("Int", (ATerm) ATmakeInt($1));}
	| REAL			      	{$$ = App1("Real", (ATerm) ATmakeReal($1));}
	| STRINGTOK			{$$ = App1("Str", $1);}
     	| id opttermlist 		{$$ = App2("Op", $1, (ATerm) $2);}
        | id AS term                    {$$ = App2("As", App1("Var", $1), $3);}
	| LT strategy GT term 		{$$ = App2("App", $2, $4);}
	| LBRACK termlist tail RBRACK   {$$ = list_to_consnil_op_tl($2, $3);}
	| LPAREN termlist RPAREN	{$$ = list_to_tconstnil_op($2);}
	| term EXPLODE LPAREN term RPAREN 
					{$$ = App2("Explode", $1, $4)}
        ;

tail    : 				{$$ = ATmake("Op(\"Nil\",[])");}
	| BAR term 			{$$ = $2;}
        ;

optcont :				{$$ = ATmakeList0();}
	| LBRACK term optcond RBRACK trav 	
					{$$ = ATmakeList2($2, App2("Call", $5, (ATerm) ATmakeList0()));}
        ;

trav    : 				{$$ = ATmake("SVar(\"oncetd\")"); }
	| LPAREN id RPAREN 		{$$ = App1("SVar", $2);}

opttermlist
	: 				{$$ = ATmakeList0();}
	| LPAREN termlist RPAREN 	{$$ = $2;}
        ;

termlist: term				{$$ = ATmakeList1($1);}
	| term COMMA termlist  		{$$ = ATinsert($3, $1);}
        |				{$$ = ATmakeList0();}
        ;

tvarlist: LCID				{$$ = ATmakeList1($1);}
	| LCID COMMA tvarlist  		{$$ = ATinsert($3, $1);}
        |				{$$ = ATmakeList0();} 
        ;

/* Rewrite rules */

rules 	:           			{$$ = ATmakeList0();}
      	| rules rule_def  		{$$ = ATappend($1, $2);}
        ;

rule_def 
	: id optvarlist COLON rule	{$$ = App3("RDef", $1, (ATerm) $2, (ATerm) $4);}
	| id optvarlist 
		DOUBLECOLON stratrule	{$$ = App3("RDef", $1, (ATerm) $2, (ATerm) $4);}
        ;
       
rule    : term ARROW term optcond 	{$$ = App3("Rule", $1, $3, $4);	}
        ;

stratrule : strategy LONGARROW strategy optcond 	
					{$$ = App3("StratRule", $1, $3, $4);	}
        ;

optcond :				{$$ = ATmake("Id");}
	| WHERE strategy		{$$ = App1("Where", $2);}
        ;

/* Strategies */

optapplication 
	: 				{$$ = ATmake("Id");}
	| LEFTDOUBLEARROW strategy	{$$ = $2;}

strategy
	: id optstrategylist 		{$$ = App2("Call", App1("SVar", $1), (ATerm) $2);}

	| id MODIFIER id optstrategylist
	  {$$ = App2("Call", App1("SVar", App2("Mod", $1, $3)), (ATerm)$4);}

	| strategy EXPLODE LPAREN strategy RPAREN 
					{$$ = App2("ExplodeCong", $1, $4)}

	| MATCH LPAREN term RPAREN	{$$ = App1("Match", $3);}

	| MATCH LPAREN term RPAREN	{$$ = App1("Match", $3);}

	| MATCHe term optapplication	{$$ = App2("AM", $3, $2);}

	/* | MATCHe term 		{$$ = App1("Match", $2);} */

	| BUILD  LPAREN term RPAREN	{$$ = App1("Build", $3);}
	| BUILDe term			{$$ = App1("Build", $2);}

	| ANNMATCH LPAREN term COMMA term RPAREN 
					{$$ = App2("AnnMatch", $3, $5);}
	| ANNBUILD LPAREN term COMMA term RPAREN 
					{$$ = App2("AnnBuild", $3, $5);}
	| ANNRM term                    {$$ = App1("AnnRemove", $2);}

	| NEW    			{$$ = ATmake("Prim(\"new\")");}
	| STR_GT    			{$$ = ATmake("Prim(\"str_gt\")");}
	| KIDS    			{$$ = ATmake("Prim(\"kids\")");}
        | PRIM LPAREN STRINGTOK RPAREN  {$$ = App1("Prim", $3);}

        | LL rule GG			{$$ = App1("SRule", $2);}
        | BACKSLASH rule BACKSLASH	{$$ = App1("LRule", $2);}
	| LT strategy GT term		{$$ = App2("BA", $2, $4);}

	| strategy DOUBLEARROW term	{$$ = App2("AM", $1, $3);}

	/* | term ASSIGN term		{$$ = App2("AM", $1, App1("Build", $3));} */
        | FAIL 				{$$ = ATmake("Fail");}
        | SUCC 				{$$ = ATmake("Id");}
        | INT strategy			{$$ = App2("Path", (ATerm) ATmakeInt($1), $2); }
        | NOT  LPAREN strategy RPAREN	{$$ = App1("Not", $3);}
        | WHERE LPAREN strategy	RPAREN	{$$ = App1("Where", $3);}
        | TEST  LPAREN strategy RPAREN	{$$ = App1("Test", $3);}
	| LCURLY tvarlist COLON strategy RCURLY 	
					{$$ = App2("Scope", (ATerm) $2, $4);}
	| strategy SEMICOLON strategy	{$$ = App2("Seq", $1, $3);}
	| strategy PLUS strategy 	{$$ = App2("Choice", $1, $3);}
        | strategy LTPLUS strategy   	{$$ = App2("LChoice", $1, $3);}
	| MU LCID LPAREN strategy RPAREN{$$ = App2("Rec", $2, $4);}
	| SOMETOK LPAREN strategy RPAREN{$$ = App1("Some", $3);}
	| ONE LPAREN strategy RPAREN	{$$ = App1("One", $3);}
	| ALL LPAREN strategy RPAREN	{$$ = App1("All", $3);}
	| THREAD LPAREN strategy RPAREN	{$$ = App1("Thread", $3);}
	| LPAREN strategylist RPAREN	{$$ = tuple_cong($2);}
	| LBRACK strategylist 
		 strategytail RBRACK	{$$ = list_cong($2, $3);}
	| STRINGTOK			{$$ = App1("Match", App1("Str", $1));}
	| INT			      	{$$ = App1("Match", 
					      App1("Int", (ATerm) ATmakeInt($1)));}
	| REAL			      	{$$ = App1("Match", 
					      App1("Real", (ATerm) ATmakeReal($1)));}
        ;

strategytail 
	: 				{$$ = ATmake("Call(SVar(\"Nil\"),[])");}
	| BAR strategy			{$$ = $2;}
	;

optstrategylist
	:			     	{$$ = ATmakeList0();}
	| LPAREN strategylist RPAREN 	{$$ = $2;}
        ;

strategylist 
	: 				{$$ = ATmakeList0();}
	| strategy 			{$$ = ATmakeList1($1);}
	| strategy COMMA strategylist 	{$$ = ATinsert($3, $1);}
        ;

/* Strategy definitions */ 

strategies 
	:                         	{$$ = ATmakeList0();}
	| strategies strategy_def 	{$$ = ATappend($1, $2);}
        ;

strategy_def 
	: id optvarlist EQ strategy	{$$ = App3("SDef", $1, (ATerm) $2, $4);}
        ;

optvarlist
	: 				{$$ = ATmakeList0();}
	| LPAREN varlist RPAREN 	{$$ = $2;}
        ;

varlist : 				{$$ = ATmakeList0();}
	| id				{$$ = ATmakeList1($1);}
	| id COMMA varlist 		{$$ = ATinsert($3, $1);}
        ;

overlays:				{$$ = ATmakeList0();}
	| overlay overlays 		{$$ = ATinsert($2, $1);}
        ;

overlay : id LPAREN varlist RPAREN EQ term
					{$$ = App3("Overlay", $1, (ATerm) $3, $6);}
	| id EQ term 	{$$ = App3("Overlay", $1, (ATerm) ATmakeList0(), $3);}
        ; 

/* Note this allows contexts in overlay definitions; should be excluded
by checking in front-end */